configurations {
	jaxb
}

buildscript {
	ext {
		springBootVersion = '2.0.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'pl.edu.agh.sp'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

// tag::wsdl[]
task genJaxb {
	ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
	ext.classesDir = "${buildDir}/classes/jaxb"
	ext.schema = "https://webapi.allegro.pl/service.php?wsdl"

	outputs.dir classesDir

	doLast() {
		project.ant {
			taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
					classpath: configurations.jaxb.asPath
			mkdir(dir: sourcesDir)
			mkdir(dir: classesDir)

			xjc(destdir: sourcesDir, schema: schema,
					package: "api.allegro.wsdl") {
				arg(value: "-wsdl")
				produces(dir: sourcesDir, includes: "**/*.java")
			}

			javac(destdir: classesDir, source: 1.8, target: 1.8, debug: true,
					debugLevel: "lines,vars,source",
					classpath: configurations.jaxb.asPath) {
				src(path: sourcesDir)
				include(name: "**/*.java")
				include(name: "*.java")
			}

			copy(todir: classesDir) {
				fileset(dir: sourcesDir, erroronmissingdir: false) {
					exclude(name: "**/*.java")
				}
			}
		}
	}
}
// end::wsdl[]

sourceCompatibility = 1.8
targetCompatibility = 1.8

bootJar {
	baseName = 'shopping-advisor'
	version = '0.1.0'
}

repositories {
	mavenCentral()
	maven { url "https://repository.jboss.org/nexus/content/repositories/releases" }
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile("org.springframework.ws:spring-ws-core")
	compile(files(genJaxb.classesDir).builtBy(genJaxb))

	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile group: 'org.testng', name: 'testng', version: '6.14.2'

	compile("org.springframework.boot:spring-boot-starter-data-jpa")
	compile group: 'org.postgresql', name: 'postgresql', version: '42.1.4'

	// jsoup HTML parser library @ https://jsoup.org/
	compile 'org.jsoup:jsoup:1.11.3'

	jaxb "com.sun.xml.bind:jaxb-xjc:2.1.7"
}

task afterEclipseImport {
	dependsOn genJaxb
}